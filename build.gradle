plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'com.gremlin'
version '1.2'

repositories {
    mavenCentral()
    maven { url "https://plugins.gradle.org/m2/" }
}

java {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    withSourcesJar()
    withJavadocJar()
}


dependencies {
    implementation 'org.yaml:snakeyaml:1.33'
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'org.slf4j:slf4j-simple:2.0.6'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.12.7'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.7.0'
    //testImplementation 'junit:junit:4.13'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.7.0'
    testImplementation 'org.mockito:mockito-core:2.23.0'
    testImplementation 'com.github.tomakehurst:wiremock-standalone:2.27.2'
}

test {
    useJUnitPlatform()
}

tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = "com.gremlin"
            artifactId = "failure-flags-java"
            version = "1.2"
            from components.java

            pom {
                name = 'Failure Flags Java library'
                description = 'A Java library to inject failures in your application dependencies'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            authentication {
                awsIm(AwsImAuthentication)
            }
            if(project.version.endsWith('-SNAPSHOT')) {
                url "s3://maven.staging.gremlin.com"
            } else {
                url "s3://maven.gremlin.com"
            }
        }
    }
}
